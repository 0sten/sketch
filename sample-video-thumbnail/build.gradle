apply plugin: 'com.android.application'
apply from: '../build_kotlin.gradle'

Properties localProperties = openProperties("local.properties")
String jksFileName = localProperties.getProperty("sample.storeFile")
File jksFile = jksFileName != null && "" != jksFileName.trim() ? file(jksFileName) : null

android {
    compileSdkVersion COMPILE_SDK_VERSION.toInteger()

    defaultConfig {
        applicationId "me.panpf.sketch.sample.videothumbnail"

        minSdkVersion MIN_SDK_VERSION.toInteger()
        targetSdkVersion TARGET_SDK_VERSION.toInteger()
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME

        ndk {
            abiFilters "armeabi", 'x86'
        }
    }

    signingConfigs {
        release {
            storeFile jksFile
            storePassword localProperties.getProperty('sample.storePassword')
            keyAlias localProperties.getProperty('sample.keyAlias')
            keyPassword localProperties.getProperty('sample.keyPassword')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':sketch')

    implementation "androidx.appcompat:appcompat:$ANDROIDX_APPCOMPAT"
    implementation "androidx.recyclerview:recyclerview:$ANDROIDX_RECYCLERVIEW"
    implementation "androidx.constraintlayout:constraintlayout:$ANDROIDX_CONSTRAINTLAYOUT"

    implementation "androidx.lifecycle:lifecycle-extensions:$ANDROIDX_LIFECYCLE"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$ANDROIDX_LIFECYCLE"
    implementation "androidx.lifecycle:lifecycle-livedata:$ANDROIDX_LIFECYCLE"
    kapt "androidx.lifecycle:lifecycle-compiler:$ANDROIDX_LIFECYCLE"
    implementation "androidx.paging:paging-runtime:$ANDROIDX_PAGING"

    implementation "me.panpf:assembly-adapter:$ASSEMBLY_ADAPTER_VERSION"
    implementation "me.panpf:assembly-adapter-ktx:$ASSEMBLY_ADAPTER_VERSION"
    implementation "me.panpf:assembly-paged-list-adapter:$ASSEMBLY_ADAPTER_VERSION"
    implementation "me.panpf:androidx-kt:$PANPF_ANDROIDX"
    implementation "me.panpf:androidx-kt-arch:$PANPF_ANDROIDX"

    implementation "com.github.wseemann:FFmpegMediaMetadataRetriever:$FFMPEG_MEDIA_METADATA_RETRIEVER_VERSION"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$LEAK_CANARY_ANDROID_VERSION"
    debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:$LEAK_CANARY_ANDROID_VERSION"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$LEAK_CANARY_ANDROID_VERSION"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$LEAK_CANARY_ANDROID_VERSION"
}

Properties openProperties(String fileName) {
    File propertiesFile = new File(projectDir, fileName)
    if (!propertiesFile.exists()) {
        return new Properties()
    }
    FileInputStream inputStream = new FileInputStream(propertiesFile)
    Properties properties = new Properties()
    properties.load(inputStream)
    inputStream.close()
    return properties
}
